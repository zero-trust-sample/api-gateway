plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'org.jetbrains.gradle.plugin.idea-ext'
}

application {
    mainClass = 'com.zerotrust.apigateway.App'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    group = 'com.zerotrust.apigateway'

    dependencies {
        implementation platform("org.springframework.boot:spring-boot-dependencies:${spring_boot_version}")
        implementation platform("org.springframework.cloud:spring-cloud-dependencies:${spring_cloud_version}")
        implementation platform('com.google.cloud:libraries-bom:26.39.0')
        implementation 'org.springframework.boot:spring-boot-starter-web'
    }

    test {
        useJUnitPlatform()
    }
}
task jacocoRootReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("/build/jacoco/*.exec")

    subprojects.each { project ->
        sourceSets project.sourceSets.main
    }

    reports {
        xml.required = true
        html.required = true
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, exclude: '/generated/**')
                })
        )
    }
}

wrapper {
    gradleVersion = "8.7"
    distributionType = Wrapper.DistributionType.ALL
}